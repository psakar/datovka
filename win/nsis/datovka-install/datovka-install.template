!define VERSION "VERSIONXXX"

# The NSIS (http://nsis.sourceforge.net) install script.
#
# Files: nsis/datovka-install/datovka-install.template
# Copyright: 1999-2015 Nullsoft Scriptable Install System Contributors
#            2014-2015 CZ.NIC, z.s.p.o.
# License: Zlib
#
# This software is provided 'as-is', without any express or implied
# warranty. In no event will the authors be held liable for any damages
# arising from the use of this software.
#
# Permission is granted to anyone to use this software for any purpose,
# including commercial applications, and to alter it and redistribute it
# freely, subject to the following restrictions:
#
# 1. The origin of this software must not be misrepresented; you must not
#   claim that you wrote the original software. If you use this software
#   in a product, an acknowledgement in the product documentation would be
#   appreciated but is not required.
# 2. Altered source versions must be plainly marked as such, and must not be
#   misrepresented as being the original software.
# 3. This notice may not be removed or altered from any source distribution.


SetCompressor /solid /final lzma

!include LogicLib.nsh
!include MUI2.nsh
!include "FileFunc.nsh"

!define QUADVERSION "${VERSION}.0"
!define guid '{C1B3CE89-4773-4FF3-BFF7-12144DEF2F15}'
!define PROGRAM_NAME "Datovka"

outFile "./../../../datovka-${VERSION}-windows.exe"
Name "${PROGRAM_NAME} ${VERSION}"

# default install directory
installDir "$PROGRAMFILES\CZ.NIC\${PROGRAM_NAME}"
installDirRegKey HKLM "Software\${PROGRAM_NAME}" "InstallLocation"
RequestExecutionLevel admin

#give credits to Nullsoft: BrandingText ""
VIAddVersionKey "ProductName" "${PROGRAM_NAME} ${VERSION}"
VIAddVersionKey "CompanyName" "CZ.NIC, z. s. p. o."
VIAddVersionKey "FileDescription" "(un)install the ${PROGRAM_NAME} ${VERSION}"
VIAddVersionKey "LegalCopyright" "Copyright 2014 - 2015, CZ.NIC, z. s. p. o."
VIAddVersionKey "FileVersion" "${QUADVERSION}"
VIAddVersionKey "ProductVersion" "${QUADVERSION}"
VIProductVersion "${QUADVERSION}"

# Global Variables
Var StartMenuFolder

# use ReserveFile for files required before actual installation
# makes the installer start faster
#ReserveFile "System.dll"
#ReserveFile "NsExec.dll"

!define MUI_ICON "datovka.ico"
!define MUI_UNICON "datovka.ico"
!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_RIGHT
!define MUI_HEADERIMAGE_BITMAP ".\..\common\setup_top.bmp"
!define MUI_WELCOMEFINISHPAGE_BITMAP ".\..\common\setup_left.bmp"
!define MUI_UNWELCOMEFINISHPAGE_BITMAP ".\..\common\setup_left.bmp"
!define MUI_ABORTWARNING
!define MUI_WELCOMEPAGE_TITLE_3LINES
!define MUI_FINISHPAGE_TITLE_3LINES
;!define MUI_WELCOMEPAGE_TEXT "This wizard will guide you through the installation of ${PROGRAM_NAME} ${VERSION}.$\r$\n$\nNote: It is recommended to close all running ${PROGRAM_NAME} windows before proceeding with the installation of the add-on.$\r$\n$\r$\nClick Next to continue."
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "./../../../datovka.built/COPYING"
!insertmacro MUI_PAGE_DIRECTORY

!define MUI_STARTMENUPAGE_REGISTRY_ROOT "HKLM"
!define MUI_STARTMENUPAGE_REGISTRY_KEY "Software\${PROGRAM_NAME}"
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "Start Menu Folder"
!define MUI_STARTMENUPAGE_DEFAULTFOLDER "CZ.NIC\${PROGRAM_NAME}"
!insertmacro MUI_PAGE_STARTMENU DatovkaStartMenu $StartMenuFolder

!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH
!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH

!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "Czech"

section "-hidden.postinstall"

SetShellVarContext all

	# copy files
	setOutPath $INSTDIR
	File /r ".\..\..\..\datovka.built\*"
	File ".\datovka.ico"

	# store installation folder
	WriteRegStr HKLM "Software\${PROGRAM_NAME}" "InstallLocation" "$INSTDIR"
	# register uninstaller
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROGRAM_NAME}" "DisplayName" "${PROGRAM_NAME}"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROGRAM_NAME}" "UninstallString" "$\"$INSTDIR\uninst.exe$\""
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROGRAM_NAME}" "QuietUninstallString" "$\"$INSTDIR\uninst.exe$\" /S"
	WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROGRAM_NAME}" "NoModify" "1"
	WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROGRAM_NAME}" "NoRepair" "1"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROGRAM_NAME}" "URLInfoAbout" "https://labs.nic.cz/page/2425/nova-datovka/"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROGRAM_NAME}" "Publisher" "CZ.NIC, z. s. p. o."
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROGRAM_NAME}" "Version" "${VERSION}"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROGRAM_NAME}" "DisplayVersion" "${VERSION}"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROGRAM_NAME}" "Contact" "CZ.NIC, z. s. p. o."
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROGRAM_NAME}" "DisplayIcon" "$\"$INSTDIR\datovka.ico$\""
	${GetSize} "$INSTDIR" "/S=0K" $0 $1 $2
	IntFmt $0 "0x%08X" $0
	WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROGRAM_NAME}" "EstimatedSize" "$0"
	WriteUninstaller "uninst.exe"

	# start menu items
	!insertmacro MUI_STARTMENU_WRITE_BEGIN DatovkaStartMenu
	CreateDirectory "$SMPROGRAMS\$StartMenuFolder"
	CreateShortCut "$SMPROGRAMS\$StartMenuFolder\Uninstall.lnk" "$INSTDIR\uninst.exe" "" "" "" "" "" "Uninstall ${PROGRAM_NAME}"
	CreateShortCut "$SMPROGRAMS\$StartMenuFolder\Datovka.lnk" "$INSTDIR\datovka.exe" "" "" "" "" "" "Run ${PROGRAM_NAME}"
	!insertmacro MUI_STARTMENU_WRITE_END

	CreateShortCut "$DESKTOP\${PROGRAM_NAME}.lnk" "$INSTDIR\${PROGRAM_NAME}.exe" "" "$INSTDIR\datovka.ico" 0

sectionEnd

# setup macros for uninstall functions.
!ifdef UN
!undef UN
!endif
!define UN "un."

# uninstaller section
section "un.Unbound"

SetShellVarContext all

	# deregister uninstall
	DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROGRAM_NAME}"
	RMDir /r "$INSTDIR\*" 
	Delete "$DESKTOP\${PROGRAM_NAME}.lnk"
	Delete "$INSTDIR\uninst.exe"   # delete self
	RMDir "$PROGRAMFILES\CZ.NIC\${PROGRAM_NAME}"
	RMDir "$PROGRAMFILES\CZ.NIC"
	RMDir "$INSTDIR"

	# start menu items
	!insertmacro MUI_STARTMENU_GETFOLDER DatovkaStartMenu $StartMenuFolder
	Delete "$SMPROGRAMS\$StartMenuFolder\Uninstall.lnk"
	Delete "$SMPROGRAMS\$StartMenuFolder\Datovka.lnk"
	RMDir "$SMPROGRAMS\CZ.NIC\${PROGRAM_NAME}"
	RMDir "$SMPROGRAMS\CZ.NIC\"
	RMDir "$SMPROGRAMS\$StartMenuFolder"
	DeleteRegKey HKLM "Software\${PROGRAM_NAME}"

sectionEnd

Function .onInit

	;Detect previous version of Datovka
	ReadRegStr $R0 HKLM \
	"Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROGRAM_NAME}" \
	"UninstallString"

	${If} $R0 != ""
		${If} $LANGUAGE == ${LANG_ENGLISH}
			MessageBox MB_OK|MB_ICONEXCLAMATION \
			"Application ${PROGRAM_NAME} is already installed on your computer. $\n$\nClick OK to uninstall the application from the system and to start the installation of the version ${VERSION}." \
			IDOK uninst
		${EndIf}

		${If} $LANGUAGE == ${LANG_CZECH}
			MessageBox MB_OK|MB_ICONEXCLAMATION \
			"Aplikace ${PROGRAM_NAME} je již na Vašem poèítaèi nainstalována. $\n$\nStisknìte OK pro odinstalování aplikace ze systému a zahájení instalace nové verze ${VERSION}." \
			IDOK uninst
		${EndIf}

	${EndIf}

	;Detect if MSI version exists
	EnumRegKey $R1 HKLM "Software\WOW6432Node\CZ.NIC, z.s.p.o." 0

	${If} $R1 != ""
		${If} $LANGUAGE == ${LANG_ENGLISH}
			MessageBox MB_OK|MB_ICONEXCLAMATION \
			"The application $R1, which has been installed using the MSI installation package, has been detected. Before using this installer, you must manually uninstall the currently installed version." \
			IDOK exit
		${EndIf}

		${If} $LANGUAGE == ${LANG_CZECH}
			MessageBox MB_OK|MB_ICONEXCLAMATION \
			"Byla detekována aplikace $R1, která byla nainstalována pomocí MSI instalaèního balíèku. Pøed použitím tohoto instalátoru musíte souèasnì nainstalovanou verzi ruènì odinstalovat." \
			IDOK exit
		${EndIf}

	${EndIf}

	;Run the uninstaller
	uninst:
		ClearErrors
		${If} $R0 != ""
			ExecWait '$R0 _?=$INSTDIR' ;Do not copy the uninstaller to a temp file
		${EndIf}
		
		IfErrors no_remove_uninstaller done
	
	no_remove_uninstaller:
	
	done:

	;Quit installer
	exit:
		${If} $R1 != ""
			Abort
		${EndIf}

FunctionEnd
