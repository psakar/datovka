
QT += core network sql
CONFIG += console

TEMPLATE = app
APP_NAME = datovka-cli

include(pri/version.pri)
include(pri/check_qt_version.pri)

sufficientQtVersion(5, 2, 3, 2)

isEmpty(MOC_DIR) {
	MOC_DIR = gen_moc
}
isEmpty(OBJECTS_DIR) {
	OBJECTS_DIR = gen_objects
}
isEmpty(UI_DIR) {
	UI_DIR = gen_ui
}
CONFIG += object_parallel_to_source

DEFINES += \
	DEBUG=1 \
	VERSION=\\\"$${VERSION}\\\"

QMAKE_CXXFLAGS = \
	-g -O0 -std=c++11 \
	-Wall -Wextra -pedantic \
	-Wdate-time -Wformat -Werror=format-security

#INCLUDEPATH +=

LIBS = \
	-lisds

isEqual(STATIC, 1) {
	warning(Linking statically.)
} else {
	INCLUDEPATH += \
		/usr/include/libxml2
}

!isEmpty(PORTABLE_APPLICATION) {
	warning(Building portable version.)
	DEFINES += PORTABLE_APPLICATION=1
	TARGET = $${APP_NAME}-portable
}

!isEmpty(DISABLE_VERSION_CHECK_BY_DEFAULT) {
	warning(Disabling version check by default.)
	DEFINES += DISABLE_VERSION_CHECK_BY_DEFAULT=1
}

win32 {
        RC_FILE += res/datovka.rc

	VERSION_COMMAS=$$replace(VERSION, '\.', ',')
	DEFINES += \
		WIN32=1 \
		VERSION_COMMAS=$${VERSION_COMMAS} \
		INRC_FILEDESCR=\\\"Datovka-CLI\\\" \
		INRC_ORIG_FNAME=\\\"$${APP_NAME}.exe\\\"

	INCLUDEPATH = \
		mingw32built/include/libxml2 \
		mingw32built/include/

	LIBS = \
		$${_PRO_FILE_PWD_}/mingw32built/bin/libisds-5.dll \
		$${_PRO_FILE_PWD_}/mingw32built/bin/libeay32.dll

	SOURCES += src/compat/compat_win.c

	HEADERS += src/compat/compat_win.h
} else {
	LIBS += \
		-lcrypto
}

SOURCES += \
	src/about.cpp \
	src/cli/cli.cpp \
	src/cli/cli_login.cpp \
	src/cli/cli_parser.cpp \
	src/cli/cli_pin.cpp \
	src/common.cpp \
	src/crypto/crypto.c \
	src/crypto/crypto_threads.cpp \
	src/crypto/crypto_version.cpp \
	src/datovka_shared/crypto/crypto_pin.c \
	src/datovka_shared/crypto/crypto_pwd.c \
	src/datovka_shared/crypto/crypto_wrapped.cpp \
	src/datovka_shared/io/records_management_db.cpp \
	src/datovka_shared/io/sqlite/db.cpp \
	src/datovka_shared/io/sqlite/db_single.cpp \
	src/datovka_shared/io/sqlite/table.cpp \
	src/datovka_shared/isds/account_interface.cpp \
	src/datovka_shared/isds/box_interface.cpp \
	src/datovka_shared/isds/error.cpp \
	src/datovka_shared/isds/internal_conversion.cpp \
	src/datovka_shared/isds/message_interface.cpp \
	src/datovka_shared/isds/type_conversion.cpp \
	src/datovka_shared/localisation/localisation.cpp \
	src/datovka_shared/settings/pin.cpp \
	src/datovka_shared/settings/records_management.cpp \
	src/datovka_shared/utility/strings.cpp \
	src/datovka_shared/worker/pool.cpp \
	src/global.cpp \
	src/initialisation.cpp \
	src/io/account_db.cpp \
	src/io/db_tables.cpp \
	src/io/dbs.cpp \
	src/io/file_downloader.cpp \
	src/io/filesystem.cpp \
	src/io/isds_helper.cpp \
	src/io/isds_login.cpp \
	src/io/isds_sessions.cpp \
	src/io/message_db.cpp \
	src/io/message_db_set.cpp \
	src/io/message_db_set_container.cpp \
	src/io/message_db_set_delegated.cpp \
	src/io/tag_db.cpp \
	src/isds/account_conversion.cpp \
	src/isds/box_conversion.cpp \
	src/isds/error_conversion.cpp \
	src/isds/internal_type_conversion.cpp \
	src/isds/message_conversion.cpp \
	src/isds/message_functions.cpp \
	src/isds/services_account.cpp \
	src/isds/services_box.cpp \
	src/isds/services_login.cpp \
	src/isds/services_message.cpp \
	src/isds/session.cpp \
	src/isds/to_text_conversion.cpp \
	src/isds/type_description.cpp \
	src/log/log.cpp \
	src/log/log_c.cpp \
	src/log/log_device.cpp \
	src/main_cli.cpp \
	src/model_interaction/account_interaction.cpp \
	src/settings/account.cpp \
	src/settings/accounts.cpp \
	src/settings/preferences.cpp \
	src/settings/proxy.cpp \
	src/settings/registry.cpp \
	src/single/single_instance.cpp \
	src/worker/task.cpp \
	src/worker/task_download_message.cpp \
	src/worker/task_download_message_list.cpp \
	src/worker/task_download_owner_info.cpp \
	src/worker/task_download_password_info.cpp \
	src/worker/task_download_user_info.cpp \
	src/worker/task_search_owner.cpp \
	src/worker/task_send_message.cpp

HEADERS += \
	src/about.h \
	src/cli/cli.h \
	src/cli/cli_login.h \
	src/cli/cli_parser.h \
	src/cli/cli_pin.h \
	src/common.h \
	src/crypto/crypto.h \
	src/crypto/crypto_funcs.h \
	src/crypto/crypto_threads.h \
	src/crypto/crypto_version.h \
	src/datovka_shared/crypto/crypto_pin.h \
	src/datovka_shared/crypto/crypto_pwd.h \
	src/datovka_shared/crypto/crypto_wrapped.h \
	src/datovka_shared/io/records_management_db.h \
	src/datovka_shared/io/sqlite/db.h \
	src/datovka_shared/io/sqlite/db_single.h \
	src/datovka_shared/io/sqlite/table.h \
	src/datovka_shared/isds/account_interface.h \
	src/datovka_shared/isds/box_interface.h \
	src/datovka_shared/isds/error.h \
	src/datovka_shared/isds/internal_conversion.h \
	src/datovka_shared/isds/message_interface.h \
	src/datovka_shared/isds/type_conversion.h \
	src/datovka_shared/isds/types.h \
	src/datovka_shared/localisation/localisation.h \
	src/datovka_shared/settings/pin.h \
	src/datovka_shared/settings/records_management.h \
	src/datovka_shared/utility/strings.h \
	src/datovka_shared/worker/pool.h \
	src/global.h \
	src/initialisation.h \
	src/io/account_db.h \
	src/io/db_tables.h \
	src/io/dbs.h \
	src/io/file_downloader.h \
	src/io/filesystem.h \
	src/io/isds_helper.h \
	src/io/isds_login.h \
	src/io/isds_sessions.h \
	src/io/message_db.h \
	src/io/message_db_set.h \
	src/io/message_db_set_container.h \
	src/io/tag_db.h \
	src/isds/account_conversion.h \
	src/isds/box_conversion.h \
	src/isds/conversion_internal.h \
	src/isds/error_conversion.h \
	src/isds/internal_type_conversion.h \
	src/isds/message_conversion.h \
	src/isds/message_functions.h \
	src/isds/services.h \
	src/isds/services_internal.h \
	src/isds/services_login.h \
	src/isds/session.h \
	src/isds/to_text_conversion.h \
	src/isds/type_description.h \
	src/log/log_c.h \
	src/log/log_common.h \
	src/log/log_device.h \
	src/log/log.h \
	src/model_interaction/account_interaction.h \
	src/settings/account.h \
	src/settings/accounts.h \
	src/settings/preferences.h \
	src/settings/proxy.h \
	src/settings/registry.h \
	src/single/single_instance.h \
	src/worker/message_emitter.h \
	src/worker/task.h \
	src/worker/task_download_message.h \
	src/worker/task_download_message_list.h \
	src/worker/task_download_owner_info.h \
	src/worker/task_download_password_info.h \
	src/worker/task_download_user_info.h \
	src/worker/task_search_owner.h \
	src/worker/task_send_message.h

FORMS +=

RESOURCES += \

TRANSLATIONS += locale/datovka_en.ts \
    locale/datovka_cs.ts

OTHER_FILES +=
